buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"
    }
}

version project.projectVersion
group "org.grails.plugins"

apply plugin: 'java-library'
apply plugin:"eclipse"
apply plugin:"idea"
apply plugin: "org.asciidoctor.jvm.convert"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.internal.grails-plugin-publish"

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    documentation
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.mongodb') {
            details.useVersion(mongodbJavaDriverVersion)
        }
    }
}

dependencies {
    api "org.springframework.boot:spring-boot-starter-logging"
    api "org.springframework.boot:spring-boot-autoconfigure"
    api "org.grails:grails-core"
    api "org.grails.profiles:plugin"
    api "org.grails:grails-gorm-testing-support"
    api("de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0") {
        exclude group: "commons-io"
        exclude group: "org.apache.commons", module: "commons-lang3"
        exclude group: "net.java.dev.jna"
        exclude group: "org.slf4j"
    }
    api "org.grails.plugins:mongodb"
    documentation "org.codehaus.groovy:groovy:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-ant:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-templates:$groovyVersion"
}

grailsPublish {
    userOrg = 'grails'
    githubSlug = 'grails-plugins/grails-embedded-mongodb'
    license {
        name = 'Apache-2.0'
    }
    title = "Embedded MongoDB Plugin"
    desc = "Executes an embedded mongo database for integration or functional testing"
    developers = [jameskleeh: "James Kleeh", puneetbehl: "Puneet Behl"]
}

asciidoctor {
    resources {
        from('src/docs/images')
        into "./images"
    }

    attributes 'experimental'  : 'true',
            'compat-mode'   : 'true',
            'toc'           : 'left',
            'icons'         : 'font',
            'version'       : project.version,
            'groupId'       : project.group,
            'artifactId'    : project.name,
            'sourcedir'     : "${projectDir}/src/main/groovy"
}

def apidocs = tasks.register("apidocs", Groovydoc) {
    group = "documentation"
    source project.files('src/main/groovy')

    destinationDir = new File(buildDir, 'docs/api')
    // Might need a classpath
    docTitle = "Embedded MongoDB Grails Plugin ${version}"

    classpath = configurations.documentation
    groovyClasspath = configurations.documentation
}

def docs = tasks.register("docs", Copy) {
    group = "documentation"
    dependsOn = ["apidocs", "asciidoctor"]
    from "$buildDir/asciidoc/html5"
    into "$buildDir/docs"
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

test {
    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
    }
}


bootJar.enabled = false
